5  HOME : VTAB 5: HTAB 8: PRINT "LOADING NATHAN"
10  ONERR  GOTO 1030
20  PRINT : POKE  - 16297,0: POKE  - 16304,0
30  DIM H(139): GOTO 720
40  REM ** CREATE THE PROFILE **
50  HCOLOR= 1: FOR I = 1 TO 278 STEP 2: HPLOT I,H(I / 2) + 1 TO I,159: NEXT
60  RETURN
70  HGR : VTAB 23: PRINT "    ONE CASTLE FOR EACH OF YOU....":N1 = 2
80 X1 = 20 +  FN D(30):X2 = 80 +  FN D(40):L(1) = 10 +  FN D(X1 - 10):L(2) = X2 +  FN D(120 - X2)
90 N = 158 -  FN D(58): FOR I = 0 TO X1:H(I) = N: NEXT :N = 1: GOSUB 600
100 N = 158 -  FN D(58): FOR I = X2 TO 139:H(I) = N: NEXT :N = 2: GOSUB 600
110  FOR KK = 1 TO 1000: NEXT KK
120 X3 = X1 +  FN D(X2 - X1 - 20) + 10:H(X3) = 50 +  FN D(100):N = H(X3) / 2:D1 = N - H(1) / 2:D2 = N - H(139) / 2
130  PRINT : PRINT "THE SCORE:  ";P$(1);: HTAB 23: PRINT " =";S(1): HTAB 13: PRINT P$(2);: HTAB 23: PRINT " =";S(2)
140  IF S(1) + S(2) = 0 THEN 160
150  VTAB 21 + WIN: HTAB 28: INVERSE : PRINT "VICTOR": NORMAL
160 A = 180:R = 180 / (X3 - X1 + 1):N = H(1) + D1
170  FOR I = X1 + 1 TO X3 - 1:A = A - R:H(I) =  COS (A * .0174533) * D1 + N: NEXT
180 A = 0:R = 180 / (X2 - X3 + 1):N = H(139) + D2
190  FOR I = X3 + 1 TO X2 - 1:A = A + R:H(I) =  COS (A * .0174533) * D2 + N: NEXT
200  HOME : PRINT "THE DAY OF RECKCONING HAS ARRIVED . . .": GOSUB 40
210 W =  FN D(100) - 50: HTAB 10: PRINT "WIND AT ";W: GOSUB 650: FOR I = 1 TO 1500: NEXT I:N = N1
220  HOME :N = 3 - N:S = 5 * N - 4: FLASH : HTAB 28 * N - 27: PRINT P$(N);: HTAB 55 - 27 * N: NORMAL : PRINT P$(3 - N);
230  FOR I = 0 TO 3: HTAB 12 + I * 4: PRINT A(S + 4 - I);: NEXT : PRINT
240  PRINT "LAST SHOTS";: FOR I = 0 TO 3: HTAB 12 + I * 4: PRINT V(S + 4 - I);: NEXT : PRINT : POKE 34,22
250  HCOLOR= N
260  INPUT "CALL YOUR SHOT - (ELEVATION, VELOCITY)? ";ANG,V
270  POKE 34,20: HOME
280  IF V < 350 THEN 310
290  CALL 768: CALL 768: PRINT : PRINT "YOUR CANNON BLEW APART ! ! !": PRINT "NEXT TIME - - WATCH IT!!"
295  FOR I = 1 TO 1000: NEXT I
300  GOTO 550
310  GOSUB 630
320  IF N = 2 THEN ANG = 180 - ANG
330 V1 =  COS (ANG * .0174533) * V:V2 =  -  SIN (ANG * .0174533) * V
340  POKE 769,129: CALL 768
350 MN = L(N):X = L(N) * 2 + 6:Y = H(MN)
360 X = X - 7 * (N = 2)
370  HPLOT X,Y:OX = 0
380 X = X + V1 / 10:V1 = V1 + (W - V1) / 30: IF X < 0 OR X > 279 THEN 220
390 Y = Y + V2 / 10:V2 = V2 + 6
400  IF Y < 1 THEN 430
410  IF OX THEN  HPLOT OX,0:OX = 0
420  HCOLOR= 7: HPLOT  TO X,Y: GOTO 440
430 OX = X: HCOLOR= N
440  IF H(X / 2) - Y > 2 THEN 380
450 Y = H(X / 2) + 2: HCOLOR= 5
460  XDRAW 1 AT X,Y
470  IF  ABS (X / 2 - L(3 - N)) < 3 THEN 510
480  IF  ABS (X / 2 - L(N)) > 3 THEN 500
490  PRINT : PRINT : CALL 768: PRINT "YOU DUMMY -- DON'T DROP YOUR AMMO!": GOTO 550
500  CALL 768: XDRAW 1 AT X,Y: XDRAW 1 AT X,Y: XDRAW 1 AT X,Y: GOTO 220
510  PRINT : FOR I = 1 TO 30 STEP 6: HTAB I: PRINT "POW";: CALL 768: NEXT : HTAB 30: CALL 768: PRINT "POWEE!!": CALL 768
520 WIN = N
530  FOR KK = 1 TO 1000: NEXT KK
540 N = 3 - N
550 S(3 - N) = S(3 - N) + 1: IF S(3 - N) + S(N) = GN THEN 1040
560  FOR I = 1 TO 10:A(I) = 0:V(I) = 0: NEXT :N1 = 3 - N1: HGR : GOTO 80
570  GOTO 1040
580  END
590  REM ** DRAW THE CASTLE **
600  HCOLOR= 6:X = L(N) * 2:Y = H(L(N)) - 1: FOR I =  - 2 TO 3: HPLOT X + I,Y + 1 TO X + I,Y - 2: NEXT
610  HPLOT X - 4,Y - 4 TO X - 4,Y - 2: HPLOT X - 3,Y - 4 TO X - 3,Y - 2: HPLOT X,Y - 4 TO X,Y - 2: HPLOT X + 1,Y - 4 TO X + 1,Y - 2
620  HPLOT X + 4,Y - 4 TO X + 4,Y - 2: HPLOT X + 5,Y - 4 TO X + 5,Y - 2: RETURN
630  REM ** RECORD LAST SHOTS **
640 NN = 5 * N: FOR J = 1 TO 4:K = NN - 5 + J:A(K) = A(K + 1):V(K) = V(K + 1): NEXT :V(NN) = V:A(NN) = ANG: RETURN
650  REM ** DRAW WIND VECTOR **
660  HCOLOR= 7: HPLOT 142 - W,150 TO 143 + W,150
670 SS =  -  SGN (W)
680  FOR I = 1 TO 5:Y = 150 - I:X = 140 + W + SS * I
690  HPLOT X,Y TO X + 1,Y
700 Y = 150 + I: HPLOT X,Y TO X + 1,Y: NEXT
710  RETURN
720  TEXT : HOME : VTAB 10:A$ = "*** SUPER ARTILLERY ***": HTAB 21 -  LEN (A$) / 2
730  FOR I = 1 TO  LEN (A$): PRINT  MID$ (A$,I,1);:Z =  PEEK ( - 16336) +  PEEK ( - 16336) +  PEEK ( - 16336) +  PEEK ( - 16336): NEXT
740  FOR I = 1 TO 2000: NEXT I
750  HOME : VTAB 3:A$ = "GAME RULES ARE SIMPLE:": GOSUB 990: PRINT
760 A$ = "BLOW UP YOUR OPPONENT BY SHOOTING AT THEPROPER ANGLE AND VELOCITY,": GOSUB 990
770  PRINT :A$ = "WHILE COMPENSATING FOR WIND AND TERRAIN.": GOSUB 990
780 A$ = "EACH GUNNER MUST ENTER THE GUN ELEVATIONAND THE SHELL VELOCITY.": GOSUB 990: PRINT
790 A$ = "THE ELEVATION MUST BE BETWEEN 0 FOR A   HORIZONTAL SHOT, AND 90 FOR STRAIGHT UP.": GOSUB 990
800  VTAB 21: POKE 34,20
810  FOR P = 1 TO 2
820  PRINT "PLAYER ";P;: INPUT ", WHAT IS YOUR NAME? ";P$(P): IF  LEN (P$(P)) > 10 THEN  HOME : PRINT "PLEASE USE A NAME LESS THAN 10 LETTERS": GOTO 820
830  HOME : NEXT
840  DEF  FN D(X) =  INT (X *  RND (1))
850  INPUT "HOW MANY TOTAL GAMES DO YOU WISH TO PLAY";GN
860  IF GN < 1 THEN  PRINT "PLEASE, DON'T JOKE AROUND! ": GOTO 850
870  POKE 232,72: POKE 233,3: SCALE= 1: ROT= 0
880  FOR I = 0 TO 19
890  READ J
900  POKE I + 768,J: NEXT
910  DATA  160,0,136,208,253,173,48,192,206,1,3,206,1,3,206,1,3,208,237,96
920  FOR I = 0 TO 39
930  READ J
940  POKE I + 840,J
950  NEXT : FOR I = 1 TO 1500: NEXT I: HOME : GOTO 70
960  DATA  1,0,4,0
970  DATA  109,56,63,63,44,45,45,13,8,63,31,63,63,56,56,56
980  DATA  48,146,45,69,41,33,33,33,33,219,23,23,18,36,35,12,56,155,4,0
990  FOR I = 1 TO  LEN (A$): PRINT  MID$ (A$,I,1);
1000 Z =  PEEK ( - 16336): NEXT : PRINT
1010  FOR I = 1 TO 700: NEXT I
1020  RETURN
1030  IF  PEEK (222) <  > 255 THEN  RESUME
1040  TEXT : HOME : VTAB 10
1050  PRINT "THE SCORE:  ";P$(1);: HTAB 23: PRINT " =";S(1): HTAB 13: PRINT P$(2);: HTAB 23: PRINT " =";S(2)
1060  IF S(1) > S(2) THEN WI = 1
1070  IF S(1) < S(2) THEN WI = 2
1080  IF S(1) = S(2) THEN 1120
1090  VTAB 9 + WI: HTAB 28
1100  INVERSE : PRINT "WINNER"
1110  GOTO 1140
1120  PRINT : HTAB 13: INVERSE
1130  PRINT " TIE "
1140  NORMAL : VTAB 20
1160  PRINT "BETTER LUCK AT THE                      PEACE TALKS NEXT TIME!! "
1170  INPUT "DO YOU WANT TO PLAY AGAIN (Y/N)? ";A$
1180  IF A$ = "Y" THEN  RESTORE : HOME : POKE 34,20: HOME : FOR I = 1 TO 10:A(I) = 0:V(I) = 0: NEXT :S(1) = 0:S(2) = 0: GOTO 850
1190  IF A$ <  > "N" THEN  PRINT  CHR$ (7);: VTAB  PEEK (37): GOTO 1170
1200  TEXT : HOME : END
65535  REM  **COPYRIGHT C 1980**
65535  REM  **RAINY CITY SOFTWARE**
65535  REM  **GREG STEIN**